* Jeremy's Emacs of Emacs setup
#+NAME:   Emacs Dashboard
[[./assets/screenshot.png]]

** Installation
This is my personal emacs configuration. I prefer to keep this repo seperate from my =~/.emacs.d/= dir, therefore, the =~/.emacs= file can be generated directly from this readme by tangling the code snippets. This can be done globally with org-babel-tangle (~C-c C-v t~). After tangling the file you should be able to relaunch (or simply evaluate) the resulting =~/.emacs= file and, assuming you have a working [[https://github.com/jwiegley/use-package][use-package]] configuration, emacs should work without issue.

Before tangling the file, please read through the random thoughts throughout and turn on/off the features that you would like by removing the ~:tangle~ flag from the source blocks. Tangling the entire file will overwrite anything in your local =~/.emacs= file.

Credit where credit is due: a lot of the content in my configuration was lifted from fellow emacsers like [[https://github.com/daedreth/UncleDavesEmacs][Uncle Dave]], [[http://sachachua.com/blog/category/emacs/][Sacha Chua]] and [[http://cestlaz.github.io/stories/emacs/][Mike Zamanski]] to name a few; as well as countless reddit, emacs.stackexchange and stackoverflow contributors.

** Configuration
*** Package archives and use-package
Setting up basic repositories and such.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (require 'package)
  (setq package-enable-at-startup nil)

  (setq package-archives '(("marmalade"  . "http://marmalade-repo.org/packages/")
            ("melpa-stable" . "http://stable.melpa.org/packages/")
            ("gnu" . "https://elpa.gnu.org/packages/")
            ("melpa" . "https://melpa.org/packages/")))
  (package-initialize)

  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
  (package-install 'use-package))

#+END_SRC
*** Personal Information
For the sake of completeness, configure name and email address
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq user-full-name "Jeremy Gooch"
        user-mail-address "jeremy.gooch@gmail.com")
#+END_SRC

*** Run emacs as a replacement for desktop/window manager
Emacs was the original terminal multiplexer so manages split views quite nicely.

First, get some basics out of the way. Switch to fullscreen mode and hide the menus and also adjust the default font size while we're at it.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (set-frame-parameter nil 'fullscreen 'fullboth)
  (set-face-attribute 'default nil :height 100)
  (menu-bar-mode -1)
  (tool-bar-mode -1)
#+END_SRC

#+RESULTS:

The default keybindings for naviating split views can be cumbersome, so remap them to something a bit easier.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs

  (global-set-key (kbd "C-c <left>")  'windmove-left)
  (global-set-key (kbd "C-c <right>") 'windmove-right)
  (global-set-key (kbd "C-c <up>")    'windmove-up)
  (global-set-key (kbd "C-c <down>")  'windmove-down)
#+END_SRC

We'll need to turn off the mouse from time to time
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package disable-mouse
    :ensure t)
#+END_SRC

**** EXWM
With EXWM it can act as a complete desktop environment.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package exwm
    :ensure t)
  (use-package exwm-config
    :config
    (exwm-config-default)
    (exwm-config-ido))
#+END_SRC

Fortunately EXWM also has support for multiple monitors.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package exwm-randr)
  (setq exwm-randr-workspace-output-plist '(1 "VGA-1" 2 "VGA-2"))
  (add-hook 'exwm-randr-screen-change-hook
            (lambda ()
              (start-process-shell-command
               "xrandr" nil "xrandr --output VGA-1 --left-of VGA-2 --auto")))
  (exwm-randr-enable)
#+END_SRC

**** Screenshots
I took this from Uncle Dave's config. Its a super slick screenshot function for taking both fullscreen as well as region screenshots.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
    (defun jrm3/take-screenshot ()
    "Takes a fullscreenshot of the current workspace"
    (interactive)
    (when window-system
      (cl-loop for i downfrom 3 to 1 do
            (progn
              (message (concat (number-to-string i) "..."))
              (sit-for 1)))
      (message "Cheese!")
      (sit-for 1)
      (start-process "screenshot" nil "import" "-window" "root"
                     (concat (getenv "HOME") "/Pictures/" (current-time-string) ".png"))
      (message "Screenshot taken!")))
  (global-set-key (kbd "<C-print>") 'jrm3/take-screenshot)

  (defun jrm3/take-screenshot-region ()
    "Takes a screenshot of a region selected by the user"
    (interactive)
    (when window-system
      (call-process "import" nil nil nil ".newScreenshot.png")
      (call-process "convert" nil nil nil ".newScreenshot.png" "-shave" "1x1"
                    (concat (getenv "HOME") "/Pictures/" (current-time-string) ".png"))
      (call-process "rm" nil nil nil ".newScreenshot.png")))
  (global-set-key (kbd "<print>") 'jrm3/take-screenshot-region)
#+END_SRC

**** Application Launcher with dmenu
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(use-package dmenu
  :ensure t
  :bind
    ("s-SPC" . 'dmenu))
#+END_SRC

**** X11
According to the [[https://www.systutorials.com/docs/linux/man/1-Xorg.wrap/][man page]], edit [[/etc/X11/Xwrapper.config]] to include the line:
~allowed_users=anybody~ and ~needs_root_rights=yes~.

#+BEGIN_SRC shell :exports code :tangle ~/.xinitrc
#!/bin/bash
exec emacs
#+END_SRC

**** Minibuffer considerations for desktop/fullscreen usage
Display the current time and battery indicator
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq display-time-24hr-format t)
  (setq display-time-format "%H:%M - %d.%b.%y")
  (display-time-mode 1)

  (display-battery-mode 1)
#+END_SRC

**** Send keys to EXWM Line Mode buffers
I find the default way (C-c C-q) of exwm sending keys like ctrl to an async buffer difficult. C-c C-c is much simpler for me.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (define-key exwm-mode-map [?\C-c ?\C-c] 'exwm-input-send-next-key)
#+END_SRC

**** Mouse/Trackpad
There are a few miscellaneous modes where the mouse should be disabled by default.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (add-hook 'lisp-interaction-mode 'disable-mouse-mode)
#+END_SRC
*** UI Components
**** Dashboard
What can I say, I like a nice big splash screen and I'm partial to the meditating GNU logo.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package dashboard
    :ensure t
    :config
      (dashboard-setup-startup-hook)
      (setq dashboard-startup-banner (concat (getenv "HOME") "/dotemacs/assets/gnu-meditate-scaled.png"))
      (setq dashboard-items '((recents  . 5)
                              (projects . 5)))
      (setq dashboard-banner-logo-title ""))

#+END_SRC

**** Theme
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package zerodark-theme
    :ensure t)
#+END_SRC

**** Clean up modeline with diminish
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package diminish
    :ensure t
    :init
    (defun jrm3/diminish-modeline ()
      "Run the diminish mode line"
    (interactive)
    (diminish 'disable-mouse-mode)
    (diminish 'ivy-mode)
    (diminish 'eldoc-mode)
    (diminish 'projectile-mode))
    (jrm3/diminish-modeline))
#+END_SRC
*** Shell
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (add-hook 'shell-mode-hook 'disable-mouse-mode)
  ;; Make shells interactive (i.e. M-!, or source blocks in org)
  (setq shell-command-switch "-ic")
#+END_SRC

By default just use bash for all shells
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defvar my-term-shell "/bin/bash")
  (defadvice ansi-term (before force-bash)
    (interactive (list my-term-shell)))
  (ad-activate 'ansi-term)
#+END_SRC

*** Org Mode
Load some basic minor modes by default
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs 
  (add-hook 'org-mode-hook
            (lambda ()
              (disable-mouse-mode)
              (no-trailing-whitespace)
              ))
  (add-hook 'org-src-mode-hook 'disable-mouse-mode)
#+END_SRC

Show the asterisks as bullets
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(use-package org-bullets
  :ensure t
  :config
    (add-hook 'org-mode-hook (lambda () (org-bullets-mode))))
#+END_SRC

Add some export modes for getting content out of org
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package ox-twbs
    :ensure t)
#+END_SRC
**** Literate programming
One of the coolest features of org mode imo is the ability to evaluate almost any language via org-babel. Lets load in the non-common ones.
#+BEGIN_SRC emacs-lisp :exports code ~/.emacs
  (add-to-list 'org-src-lang-modes '("js" . "javascript")
               '("php" . "php"))

  (org-babel-do-load-languages
   'org-babel-load-languages
   '((python . t)
     (go . t)
     (typescript . t)
     (rust . t)
     (sql . t)
     (java . t)))
#+END_SRC

Make it easier to tangle the current source block
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(global-set-key (kbd "C-c v t") (lambda () (interactive) (org-babel-tangle-block)))
#+END_SRC

Add option to append as part of tangling a file
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun org-babel-tangle-append ()
    "Append source code block at point to its tangle file.
  The command works like `org-babel-tangle' with prefix arg
  but `delete-file' is ignored."
    (interactive)
    (cl-letf (((symbol-function 'delete-file) #'ignore))
      (org-babel-tangle '(4))))

  (defun org-babel-tangle-append-setup ()
    "Add key-binding C-c C-v C-t for `org-babel-tangle-append'."
    (org-defkey org-mode-map (kbd "C-c C-v +") 'org-babel-tangle-append))

  (add-hook 'org-mode-hook #'org-babel-tangle-append-setup)
#+END_SRC

**** Org Agenda
Bind org agenda to shortcut and give it our todo paths. Note that the paths are not recursive for org-agenda-files.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (define-key global-map "\C-ca" 'org-agenda)

  ;; use find-file for recursion
  (load-library "find-lisp")
  (setq org-agenda-files (find-lisp-find-files "~/Dropbox" "\.org$"))

  ;; Exclude the dropbox cache directory
  (eval-when-compile (require 'cl))
  (setq org-agenda-files
        (remove-if '(lambda (x)
                      (string-match
                       (concat "^" (regexp-quote (expand-file-name "~/Dropbox/.dropbox.cache/")))
                       x))
                   org-agenda-files))
#+END_SRC

Super simple reordering from Org Agenda -> Todo view
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs

  ;; The following was lifted from http://pragmaticemacs.com/emacs/reorder-todo-items-in-your-org-mode-agenda/
  (defun jrm3/org-headline-to-top ()
    "Move the current org headline to the top of its section"
    (interactive)
    ;; check if we are at the top level
    (let ((lvl (org-current-level)))
      (cond
       ;; above all headlines so nothing to do
       ((not lvl)
        (message "No headline to move"))
       ((= lvl 1)
        ;; if at top level move current tree to go above first headline
        (org-cut-subtree)
        (beginning-of-buffer)
        ;; test if point is now at the first headline and if not then
        ;; move to the first headline
        (unless (looking-at-p "*")
          (org-next-visible-heading 1))
        (org-paste-subtree))
       ((> lvl 1)
        ;; if not at top level then get position of headline level above
        ;; current section and refile to that position. Inspired by
        ;; https://gist.github.com/alphapapa/2cd1f1fc6accff01fec06946844ef5a5
        (let* ((org-reverse-note-order t)
               (pos (save-excursion
                      (outline-up-heading 1)
                      (point)))
               (filename (buffer-file-name))
               (rfloc (list nil filename nil pos)))
          (org-refile nil nil rfloc))))))

  (defun jrm3/org-agenda-item-to-top ()
    "Move the current agenda item to the top of the subtree in its file"
    (interactive)
    ;; save buffers to preserve agenda
    (org-save-all-org-buffers)
    ;; switch to buffer for current agenda item
    (org-agenda-switch-to)
    ;; move item to top
    (jrm3/org-headline-to-top)
    ;; go back to agenda view
    (switch-to-buffer (other-buffer (current-buffer) 1))
    ;; refresh agenda
    (org-agenda-redo)
    )

  (define-key org-agenda-mode-map (kbd "1") 'jrm3/org-agenda-item-to-top)
#+END_SRC

Setup reminders with [[http://sachachua.com/blog/2007/11/setting-up-appointment-reminders-in-org/][org-agenda-to-appt]].
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun org-agenda-to-appt ()
    "Activate appointments found in `org-agenda-files'."
    (interactive)
    (require 'org)
    (let* ((today (org-date-to-gregorian
           (time-to-days (current-time))))
       (files org-agenda-files) entries file)
      (while (setq file (pop files))
        (setq entries (append entries (org-agenda-get-day-entries
                       file today :timestamp))))
      (setq entries (delq nil entries))
      (mapc (lambda(x)
          (let* ((event (org-trim (get-text-property 1 'txt x)))
             (time-of-day (get-text-property 1 'time-of-day x)) tod)
            (when time-of-day
          (setq tod (number-to-string time-of-day)
                tod (when (string-match
                    "\\([0-9]\\{1,2\\}\\)\\([0-9]\\{2\\}\\)" tod)
                   (concat (match-string 1 tod) ":"
                       (match-string 2 tod))))
          (if tod (appt-add tod event))))) entries)))

  (org-agenda-to-appt)
#+END_SRC

**** Org Capture
Customize org capture to my liking
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-set-key (kbd "C-c c") 'org-capture)
  (setq org-capture-templates
   '(("w" "Work Todo" entry (file "~/Dropbox/SA.org")
      "** TODO %?\n  %i\n  %a")
     ;; ("a" "Aqua Orb Todo" entry (file+headline "~/Dropbox/Projects.org" "Aqua Orb")
     ;;  "** TODO %?\n  %i\n  %a")
     ;; ("o" "Reorganize Workflow Todo" entry (file+headline "~/Dropbox/Projects.org" "Reorganize Workflow")
     ;;  "** TODO %?\n  %i\n  %a")
     ("m" "Meeting" entry (file "~/Dropbox/Meetings.org")
      "** MEETING with %? :MEETING:\n  %i\n"  :clock-in t :clock-resume t)
     ("m" "Next" entry (file "~/Dropbox/Next.org")
      "** NEXT %?\n  %i\n  %a")
     ("i" "Idea" entry (file "~/Dropbox/Ideas.org")
      "** TODO %?\n  %i\n  %a" :clock-in t :clock-resume t)
     ("M" "Milestone" entry (file "~/Dropbox/SA-milestones.org")
      "* %u %?\n*Summary*: \n\n*Description*: \n\n" :clock-in t :clock-resume t)))


#+END_SRC

**** LaTeX
LaTeX + Beamer is helpful for creating presentations, so setting up LaTeX boilerplate
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  ;; allow for export=>beamer by placing

  ;; #+LaTeX_CLASS: beamer in org files
  (unless (boundp 'org-export-latex-classes)
    (setq org-export-latex-classes nil))
  (add-to-list 'org-export-latex-classes
    ;; beamer class, for presentations
    '("beamer"
       "\\documentclass[11pt]{beamer}\n
        \\mode<{{{beamermode}}}>\n
        \\usetheme{{{{beamertheme}}}}\n
        \\usecolortheme{{{{beamercolortheme}}}}\n
        \\beamertemplateballitem\n
        \\setbeameroption{show notes}
        \\usepackage[utf8]{inputenc}\n
        \\usepackage[T1]{fontenc}\n
        \\usepackage{hyperref}\n
        \\usepackage{color}
        \\usepackage{listings}
        \\lstset{numbers=none,language=[ISO]C++,tabsize=4,
    frame=single,
    basicstyle=\\small,
    showspaces=false,showstringspaces=false,
    showtabs=false,
    keywordstyle=\\color{blue}\\bfseries,
    commentstyle=\\color{red},
    }\n
        \\usepackage{verbatim}\n
        \\institute{{{{beamerinstitute}}}}\n          
         \\subject{{{{beamersubject}}}}\n"

       ("\\section{%s}" . "\\section*{%s}")

       ("\\begin{frame}[fragile]\\frametitle{%s}"
         "\\end{frame}"
         "\\begin{frame}[fragile]\\frametitle{%s}"
         "\\end{frame}")))

    ;; letter class, for formal letters

    (add-to-list 'org-export-latex-classes

    '("letter"
       "\\documentclass[11pt]{letter}\n
        \\usepackage[utf8]{inputenc}\n
        \\usepackage[T1]{fontenc}\n
        \\usepackage{color}"

       ("\\section{%s}" . "\\section*{%s}")
       ("\\subsection{%s}" . "\\subsection*{%s}")
       ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
       ("\\paragraph{%s}" . "\\paragraph*{%s}")
       ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
#+END_SRC
**** TODO Update Custom Org workflow
Lifted from [[http://cachestocaches.com/2016/9/my-workflow-org-agenda/]]. Need to customize to my preferences.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  ;; (setq org-todo-keywords '((type "TODO" "NEXT" "DONE" "WITING" "INACTIVE" "CANCELLED" "MEETING"))

  ;; == Tags ==
  (setq org-tag-alist '((:startgroup)
                ("@errand" . ?e)
                ("@personal" . ?p)
                (:endgroup)
                ("DESIGN" . ?D)
                ("FRONTEND" . ?f)
                ("CULTURE" . ?C)
                ("DEVOPS" . ?v)
                ))

  ;; Allow setting single tags without the menu
  (setq org-fast-tag-selection-single-key 'expert)

  ;; Include the todo keywords
  (setq org-fast-tag-selection-include-todo t)

  ;; == Custom State Keywords ==
  (setq org-use-fast-todo-selection t)
  (setq org-todo-keywords
        '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d)")
      (sequence "WAITING(w@/!)" "INACTIVE(i@/!)" "|" "CANCELLED(c@/!)" "MEETING")))
  ;; Custom colors for the keywords
  (setq org-todo-keyword-faces
        '(("TODO" :foreground "red" :weight bold)
      ("NEXT" :foreground "blue" :weight bold)
      ("DONE" :foreground "forest green" :weight bold)
      ("WAITING" :foreground "orange" :weight bold)
      ("INACTIVE" :foreground "magenta" :weight bold)
      ("CANCELLED" :foregroundhttp://cachestocaches.com/2016/9/my-workflow-org-agenda/ "forest green" :weight bold)
      ("MEETING" :foreground "forest green" :weight bold)))
  ;; Auto-update tags whenever the state is changed
  (setq org-todo-state-tags-triggers
        '(("CANCELLED" ("CANCELLED" . t))
      ("WAITING" ("WAITING" . t))
      ("INACTIVE" ("WAITING") ("INACTIVE" . t))
      (done ("WAITING") ("INACTIVE"))
      ("TODO" ("WAITING") ("CANCELLED") ("INACTIVE"))
      ("NEXT" ("WAITING") ("CANCELLED") ("INACTIVE"))
      ("DONE" ("WAITING") ("CANCELLED") ("INACTIVE"))))
  (defun gs/mark-next-done-parent-tasks-todo ()
    "Visit each parent task and change NEXT (or DONE) states to TODO."
    ;; Don't change the value if new state is "DONE"
    (let ((mystate (or (and (fboundp 'org-state)
                            (member state
                    (list "NEXT" "TODO")))
                       (member (nth 2 (org-heading-components))
                   (list "NEXT" "TODO")))))
      (when mystate
        (save-excursion
          (while (org-up-heading-safe)
            (when (member (nth 2 (org-heading-components)) (list "NEXT" "DONE"))
              (org-todo "TODO")))))))
  (add-hook 'org-after-todo-state-change-hook 'gs/mark-next-done-parent-tasks-todo 'append)

#+END_SRC

**** Custom Jira Integrations for Org Agenda
Setup account
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq org-jira-working-dir "~/Dropbox/org-jira/")
  (setq jiralib-url "https://softwareadvice.atlassian.net")
#+END_SRC

Add basic group tagging to buffer
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun jrm3/jira-tags ()
    "Add Jira tags based on common Jira Ids"
    (interactive)

      (switch-to-buffer "FE.org")
      (let ((case-fold-search t))
        (goto-char (point-min))
        (while (search-forward " :fe_" nil t)
          (replace-match " :FE:FE_"))
        )
      (switch-to-buffer "GN.org")
      (let ((case-fold-search t))
        (goto-char (point-min))
        (while (search-forward " :gn_" nil t)
          (replace-match " :GN:GN_"))
        )
    )
#+END_SRC

Get issues then tag the buffers
#+BEGIN_SRC emacs-lisp :exports code ~/.emacs
  (defun jrm3/jira-get-issues ()
    "Add Jira tags based on common Jira Ids"
    (interactive)
    (org-jira-get-issues ())
    (switch-to-buffer "FE.org")
    (let ((case-fold-search t))
      (goto-char (point-min))
      (while (search-forward " :fe_" nil t)
        (replace-match " :FE:FE_"))
      )
    (switch-to-buffer "GN.org")
    (let ((case-fold-search t))
      (goto-char (point-min))
      (while (search-forward " :gn_" nil t)
        (replace-match " :GN:GN_"))
      )
    )

    (let ((case-fold-search t))
      (goto-char (point-min))
      (while (search-forward " :fe_" nil t)
        (replace-match " :FE:FE_"))

      (while (search-forward " :gn_" nil t)
        (replace-match " :GN:GN_"))
      )
    )
#+END_SRC

**** Confluence
Atlassian Confluence's WYSIWYG editor leaves a lot to be desired. It's much better to work in org mode then export to confluence using ox-confluence. At this time there is not a simple way to maintain a page in Confluence via org mode so this will have to suffice. Also, since ox-confluence is not in the repo, fetch it manually and put it in load path
#+BEGIN_SRC emacs-lisp :export code :tangle ~/.emacs
  (setq lp (concat (getenv "HOME") "/.emacs.d/lisp/"))
  (when (not (file-directory-p lp))
    (make-directory lp))

  (when (not (file-exists-p (concat lp "/ox-confluence.el")))
    (url-retrieve
     "https://raw.githubusercontent.com/emacsmirror/org/master/contrib/lisp/ox-confluence.el"
     (lambda (s)
       (re-search-forward "\r?\n\r?\n") ;; skip the headers
       (write-region (point) (point-max) (concat lp "ox-confluence.el")))))

  (add-to-list 'load-path lp)
  (load "ox-confluence")

 #+END_SRC
*** Filesystem Navigation
Tramp is a must have for my workflows
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package tramp
    :config
    (setq tramp-default-method "scp"))
#+END_SRC

I'm finding I use Tabbar less and less in favor of other utilities (i.e. ibuffer and ido). Leaving it in for now as it's sometimes still helpful in certain situations.
#+BEGIN_SRC emacs-lisp :exports code ~/.emacs
  (use-package tabbar
    :config
    (tabbar-mode t))
#+END_SRC

Setup Ibuffer and organize by mode type
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-set-key (kbd "C-x C-b") 'ibuffer)

  (setq ibuffer-saved-filter-groups
        (quote (("default"
                 ("dired" (mode . dired-mode))
                 ("org" (mode . org-mode))
                 ("shell" (mode . shell-mode))
                 ("git" (name . "^magit\*"))
                 ("Slack" (or
                           (mode . slack-mode)
                           (name . "^\\*Slack.*$")
                           ))
                 ("email" (name . "^\\*mu4e-.*\\*$"))
                 ("javascript" (or
                                (mode . javascript-mode)
                                (name . "^.*.js$")
                                (name . "^.*.json$")
                                ))
                 ("markup" (or
                            (mode . web-mode)
                            (name . "^.*.tpl")
                            (name . "^.*.mst")
                            (name . "^.*.html")
                            ))
                 ("images" (name . "^.*png$"))
                 ("process" (or
                             (mode . grep-mode)
                             (name . "^\\*tramp*$")
                             ))
                 ("emacs" (or
                           (name . "^\\*scratch\\*$")
                           (name . "^\\*Messages\\*$")
                           (name . "^\\*eww\\*$")
                           (name . "^\\*GNU Emacs\\*$")
                           ))
                 ))))

  (add-hook 'ibuffer-mode-hook
            (lambda ()
              (ibuffer-switch-to-saved-filter-groups "default")))
#+END_SRC

Dumb jump for jumping around projects
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (dumb-jump-mode)
  (global-set-key (kbd "C-c C-j") 'dumb-jump-go)
#+END_SRC

Copy current file path. Lifted from (http://ergoemacs.org/emacs/emacs_copy_file_path.html)
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
;; Copy the file path of the current buffer
(defun jrm3/copy-file-path (&optional *dir-path-only-p)
  "Copy the current buffer's file path or dired path to `kill-ring'.
Result is full path."
  (interactive "P")
  (let ((-fpath
         (if (equal major-mode 'dired-mode)
             (expand-file-name default-directory)
           (if (buffer-file-name)
               (buffer-file-name)
             (user-error "Current buffer is not associated with a file.")))))
    (kill-new
     (if *dir-path-only-p
         (progn
           (message "Directory path copied: 「%s」" (file-name-directory -fpath))
           (file-name-directory -fpath))
       (progn
         (message "File path copied: 「%s」" -fpath)
         -fpath )))))

#+END_SRC

Projectile for project level interaction
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package projectile
    :ensure t
    :config
    (projectile-global-mode))
#+END_SRC

*** In-file Navigation
Easier paragraph jumping
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-set-key (kbd "M-p") 'backward-paragraph)
  (global-set-key (kbd "M-n") 'forward-paragraph)
#+END_SRC

Avy is great for speed-of-thought navigation
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package avy
    :ensure t)
  (global-set-key (kbd "M-s") 'avy-goto-char)
  (global-set-key (kbd "C-c SPC") 'avy-goto-line)
#+END_SRC

Wrap long lines so I can see everything at a glance
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-visual-line-mode t)
#+END_SRC

*** File Editing utilities
I find it helpful to be able to backtab (shift+tab) to un-indent
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-set-key (kbd "<backtab>") 'un-indent-by-removing-4-spaces)
  (defun un-indent-by-removing-4-spaces ()
    "Remove 4 spaces from beginning of of line."
    (interactive)
    (save-excursion
      (save-match-data
        (beginning-of-line)
        ;; get rid of tabs at beginning of line
        (when (looking-at "^\\s-+")
          (untabify (match-beginning 0) (match-end 0)))
        (when (looking-at "^    ")
          (replace-match "")))))
#+END_SRC

Make evaluating lisp buffers even quicker
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (global-set-key (kbd "C-c C-e")  'eval-buffer)
#+END_SRC

Keep temporary and backup buffers out of current directory like a civilized human being.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (custom-set-variables
   '(auto-save-file-name-transforms '((".*" "~/.emacs.d/autosaves/\\1" t)))
   '(backup-directory-alist '((".*" . "~/.emacs.d/backups/")))
   '(delete-old-versions t))

  (make-directory "~/.emacs.d/autosaves/" t)
#+END_SRC

I don't mind using the minibuffer for the current line num, but vertical line numbers is helpful for pair programming situations
#+BEGIN_SRC emacs-lisp :tangle ~/.emacs
  (global-set-key (kbd "C-c l l") 'linum-mode)
  (global-set-key (kbd "C-c l d") (lambda () (interactive) (linum-mode 0)))
#+END_SRC

Turn off the mouse/trackpad when editing certain files
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (add-hook 'javascript-mode-hook 'disable-mouse-mode)
  (add-hook 'text-mode-hook 'disable-mouse-mode)
  (add-hook 'web-mode-hook 'disable-mouse-mode)
  (add-hook 'dired-mode-hook 'disable-mouse-mode)
  (add-hook 'org-mode-hoook 'disable-mouse-mode)
  (add-hook 'lisp-interaction-mode-hook 'disable-mouse-mode)
  (add-hook 'emacs-lisp-mode-hook 'disable-mouse-mode)
  (add-hook 'special-mode-hook 'disable-mouse-mode)
  (add-hook 'fundamental-mode-hook 'disable-mouse-mode)
  (add-hook 'groovy-mode-hook 'disable-mouse-mode)
  (add-hook 'ng2-ts-mode-hook 'disable-mouse-mode)
  (add-hook 'org-agenda-mode-hook 'disable-mouse-mode)
  (add-hook 'slack-message-buffer-mode-hook 'disable-mouse-mode)
#+END_SRC

**** Programming & Ops
Trailing whitespace == smh. However, I don't care about it in every situation, like when I'm reading prose.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package whitespace
    :ensure t
    :config
    (setq-default show-trailing-whitespace t)

    (defun no-trailing-whitespace ()
      (setq show-trailing-whitespace nil))

    (add-hook 'minibuffer-setup-hook
              'no-trailing-whitespace)
    (add-hook 'eww-mode-hook
              'no-trailing-whitespace)
    (add-hook 'shell-mode-hook
              'no-trailing-whitespace)
    (add-hook 'mu4e:view-mode-hook
              'no-trailing-whitespace)
    (add-hook 'help-mode-hook
              'no-trailing-whitespace)
    (add-hook 'term-mode-hook
              'no-trailing-whitespace)
    (add-hook 'slack-message-buffer-mode-hook
              'no-trailing-whitespace)
    (add-hook 'calendar-mode-hook
              'no-trailing-whitespace))

#+END_SRC

Various modes helpful for development
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs

  (use-package yaml-mode
    :ensure t
    :config
    (add-to-list 'auto-mode-alist '("\\.yml\\'" . yaml-mode)))


  (use-package restclient
    :ensure t)

  (use-package groovy-mode
    :ensure t)


  (use-package go-mode
    :ensure t)

#+END_SRC

Magit for version control
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs

  (use-package magit
    :ensure t
    :config
    (global-set-key (kbd "C-x g") 'magit-status))
#+END_SRC

Load private and workspecific configs
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs

  (defun jrm3/loadprivate ()
    "Loads configs"
    (interactive)
    (load "~/org-docs/private.el"))
#+END_SRC

Defining custom indentation based on project paths and setting them to functions that I can call as needed. This also sets backtab and backwards delete functionality.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun jrm3/setup-indent (n)
	(setq indent-tabs-mode nil)
	;; java/c/c++
	(setq-local c-basic-offset n)
	;; web development
	(setq-local coffee-tab-width n) ; coffeescript
	(setq-local javascript-indent-level n) ; javascript-mode
	(setq-local js-indent-level n) ; js-mode
	(setq-local typescript-indent-level n) ; typescript-mode
	(setq-local js2-basic-offset n) ; js2-mode, in latest js2-mode, it's alias of js-indent-level
	(setq-local web-mode-markup-indent-offset 4) ; web-mode, html tag in html file
	(setq-local web-mode-css-indent-offset 4) ; web-mode, css in html file
	(setq-local web-mode-code-indent-offset n) ; web-mode, js code in html file
	(setq-local css-indent-offset 4) ; css-mode
	)

  (defun jrm3/neon-code-style ()
	(interactive)
	(message "Using Neon coding style")
	;; indent 2 spaces width
	(jrm3/setup-indent 2))

  (defun jrm3/personal-code-style ()
	(interactive)
	(message "Using personal coding style")
	;; indent 4 spaces width
	(jrm3/setup-indent 4))

  (defun jrm3/develop-environment ()
	(interactive)
	(let ((proj-dir (file-name-directory (buffer-file-name))))
	  ;; ;; if hobby project path contains string "hobby-proj1"
	  ;; (if (string-match-p "hobby-proj1" proj-dir)
	  ;;     (my-personal-code-style))

	  ;; ;; if commericial project path contains string "commerical-proj"
	  (if (string-match-p "neon/" proj-dir)
		  (jrm3/neon-code-style))))
	  ))

  (add-hook 'prog-mode-hook 'jrm3/develop-environment)
  ;; a few major-modes does NOT inherited from prog-mode
  (add-hook 'lua-mode-hook 'jrm3/develop-environment)
  (add-hook 'web-mode-hook 'jrm3/develop-environment)



  (delete-selection-mode 1)
  ;; Backwards delete word
  (global-set-key [M-delete] 'backward-kill-word)
#+END_SRC

***** Web development
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  ;; SCSS Mode
  (use-package sass-mode
    :ensure t
    :config
    (setq exec-path (cons (expand-file-name "/usr/bin/sass") exec-path)))

  ;; SGML Mode - Indentation
  (add-hook 'sgml-mode-hook
            (lambda ()
              ;; Default indentation to 2, but let SGML mode guess, too.
              (set (make-local-variable 'sgml-basic-offset) 4)
              (sgml-guess-indent))
            )

  ;; Markdown Mode
  (autoload 'markdown-mode "markdown-mode"
    "Major mode for editing Markdown files" t)
  (add-to-list 'auto-mode-alist '("\\.text\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.markdown\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.md\\'" . markdown-mode))

  ;; Dired Mode
  (global-auto-revert-mode 1)
  (setq global-auto-revert-non-file-buffers t)
  (setq auto-revert-verbose nil)

  (use-package web-mode
    :ensure t
    :config
    (add-to-list 'auto-mode-alist '("\\.phtml\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.html\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.tpl\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.mst\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.tpl\\.php\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.[agj]sp\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.as[cp]x\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.erb\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.mustache\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.djhtml\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.scss\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.hbs\\'" . web-mode)))

  ;; Last but not least
  (setq c-basic-offset 4)
  (setq web-mode-css-indent-offset 4)
  (setq web-mode-markup-indent-offset 4)
  (setq web-mode-code-indent-offset 4)
#+END_SRC

PHP Development
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  ;; PHP Mode Improved (http://www.emacswiki.org/emacs/php-mode-improved.el)
  ;; (add-to-list 'load-path "~/.emacs.d/lisp/")
  (use-package php-mode
    :ensure t
    :config
    (autoload 'php-mode "php-mode-improved" "Major mode for editing php code." t)
    (add-to-list 'auto-mode-alist '("\\.php$" . php-mode))
    (add-to-list 'auto-mode-alist '("\\.inc$" . php-mode)))
#+END_SRC

Angular setup
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq exec-path (append exec-path '("/home/jrm3/.nvm/versions/node/v9.11.1/bin")))
  (use-package ng2-mode
    :ensure t)

  (use-package tide
    :ensure t
    :config
    (defun setup-tide-mode ()
      (interactive)
      (tide-setup)
      (flycheck-mode +1)
      (setq flycheck-check-syntax-automatically '(save mode-enabled))
      (eldoc-mode +1)
      (tide-hl-identifier-mode +1)
      ;; company is an optional dependency. 
      (company-mode +1))

    ;; aligns annotation to the right hand side
    (setq company-tooltip-align-annotations t)

    ;; formats the buffer before saving
    (add-hook 'before-save-hook 'tide-format-before-save)

    (add-hook 'typescript-mode-hook #'setup-tide-mode)
    (setq tide-format-options '(:insertSpaceAfterFunctionKeywordForAnonymousFunctions t :placeOpenBraceOnNewLineForFunctions nil :IndentStyle 2))

    (define-key tide-mode-map (kbd "C-c C-d") 'tide-documentation-at-point)
    (define-key tide-mode-map (kbd "C-c C-i") 'tide-organize-imports))
#+END_SRC

For Tide integration, use the local tsserver and fallback to the default one installed with tide
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (let* ((package-root (locate-dominating-file default-directory
                                               "package.json"))
         (path
          (and package-root
               (expand-file-name "node_modules/typescript/bin/tsserver"
                                 (expand-file-name package-root)))))
    (when (and path
               (file-exists-p path))
      (make-local-variable 'tide-tsserver-executable)
      (setq tide-tsserver-executable path)
      ))
#+END_SRC

Emmet mode for html/css
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (add-hook 'sgml-mode-hook 'emmet-mode)
  (add-hook 'css-mode-hook  'emmet-mode)
#+END_SRC

*** Consuming Content
**** Mail
Basic setup for mu4e.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs 
  (add-to-list 'load-path "/usr/share/emacs/site-lisp/mu4e/")
  (require 'mu4e)

  ;; (setq mu4e-user-mail-address-list '("jeremy@softwareadvice.com" "jeremy.gooch@gartner.com"))
  (setq mu4e-maildir "~/Maildir"
        mu4e-sent-folder "/w/[Gmail].Sent Mail"
        mu4e-drafts-folder "/w/[Gmail].Drafts"
        mu4e-trash-folder "/w/[Gmail].Trash"
        user-mail-address "jeremy@softwareadvice.com"
        smtpmail-default-smpt-server "smtp.gmail.com"
        smtpmail-local-domain "softwareadvice.com"
        smtpmail-smtp-server "smtp.gmail.com"
        smtpmail-stream-type 'starttls
        smtpmail-smtp-service 25)

  (defvar my-mu4e-account-alist
    '(("w"
       (mu4e-sent-folder "/w/[Gmail].Sent Mail")
       (mu4e-drafts-folder "/w/[Gmail].Drafts")
       (mu4e-trash-folder "/w/[Gmail].Trash")
       (user-mail-address "jeremy@softwareadvice.com")
       (smtpmail-default-smtp-server "smtp.gmail.com")
       (smtpmail-local-domain "softwareadvice.com")
       (smtpmail-smtp-user "jeremy@softwareadvice.com")
       (smtpmail-smtp-server "smtp.gmail.com")
       (smtpmail-stream-type starttls)
       (smtpmail-smtp-service 25))
      ))

  (setq mu4e-get-mail-command "offlineimap")

  ;; Assumes package gnutls-bin is installed on system
  (require 'smtpmail)
  (require 'starttls)
  (setq message-send-mail-function 'smtpmail-send-it
        starttls-gnutls-program "/usr/bin/gnutls-cli"
        starttls-extra-arguments nil
        starttls-use-gnutls t
        smtpmail-starttls-credentials '(("smtp.gmail.com" 587 nil nil))
        smtpmail-auth-credentials
        '(("smtp.gmail.com" 587 "jeremy@softwareadvice.com" nil))
        smtpmail-default-smtp-server "smtp.gmail.com"
        smtpmail-smtp-server "smtp.gmail.com"
        smtpmail-smtp-service 587)

  (setq mu4e-reply-to-address "jeremy@softwareadvice.com"
        user-mail-address "jeremy@softwareadvice.com"
        user-full-name  "Jeremy Gooch")
  (setq mu4e-compose-signature
        "Jeremy Gooch\nhttp://www.softwareadvice.com\n")

  ;; don't keep message buffers around
  (setq message-kill-buffer-on-exit t)
#+END_SRC

Setup keybinding for mu4e
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq mu4e-maildir-shortcuts
        '( ("/w/INBOX"               . ?i)
           ("/w/[Gmail].Sent Mail"   . ?s)
           ("/w/[Gmail].Spam"        . ?p)
           ("/w/[Gmail].Trash"       . ?t)
           ("/w/[Gmail].Archive"       . ?r)
           ("/w/[Gmail].Drafts"      . ?d)
           ("/w/[Gmail].All Mail"    . ?a)))

  (global-set-key (kbd "C-c m") 'mu4e)
  (global-set-key (kbd "C-c i") 'mu4e-update-index)
#+END_SRC

Add mail alerts for new messages.
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package mu4e-alert
    :ensure t
    :after mu4e
    :init
    (setq mu4e-alert-interesting-mail-query
      (concat
       "flag:unread"
       " maildir:/w/INBOX "
       ))
    ;; (mu4e-alert-set-default-style 'notifications)
    (mu4e-alert-enable-mode-line-display)
    (defun jrm3/refresh-mu4e-alert-mode-line ()
      (interactive)
      (mu4e~proc-kill)
      (mu4e-alert-enable-mode-line-display)
      )
    (run-with-timer 0 60 jrm3/refresh-mu4e-alert-mode-line)
    )

#+END_SRC

**** EWW
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun eww-new ()
    (interactive)
    (let ((url (read-from-minibuffer "Enter URL or keywords: ")))
      (switch-to-buffer (generate-new-buffer "eww"))
      (eww-mode)
      (eww url)))
#+END_SRC

*** Misc Emacs Enhancements
**** Smex for auto complete meta commands
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(use-package smex
  :ensure t
 :init
  (smex-initialize)
  :bind
  ("M-x" . smex)
  ("M-X" . smex-major-mode-commands))
#+END_SRC

**** Ido Mode
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq ido-enable-flex-matching nil)
  (setq ido-create-new-buffer 'always) ;; create a buffer with specific name if it doesn't exist already
  (setq ido-everywhere t)
  (ido-mode 1)

  (use-package ido-vertical-mode
    :ensure t)
  (ido-vertical-mode 1)
  (setq ido-vertical-define-keys 'C-n-and-C-p-only)
#+END_SRC

**** Ivy/Counsel/Swiper
Generic auto-complete with Ivy
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (ivy-mode 1)
  (use-package ivy :demand
    :ensure t
    :config
    (setq ivy-use-virtual-buffers t
          ivy-count-format "%d/%d "))
#+END_SRC

Ivy enhanced search (swiper) and common Emacs meta commands (counsel)
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(global-set-key (kbd "M-x") 'counsel-M-x)
(global-set-key (kbd "C-s") 'swiper)
#+END_SRC

**** Fix emacs' regex
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (setq-default pcre-mode t)
#+END_SRC

**** Company for auto-complete
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (add-hook 'after-init-hook 'global-company-mode)
#+END_SRC

**** Change questions to accept single letter answers
#+BEGIN_SRC emacs-lisp :exports code ~/.emacs
  (defalias 'yes-or-no-p 'y-or-n-)
#+END_SRC

**** Beacon
Helpful for finding the cursor when jumping around
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package beacon
    :ensure t
    :config
      (beacon-mode 1))
#+END_SRC
**** Which key
Some quick help for when I get stuck in the middle of a command
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (use-package which-key
    :ensure t
    :config
      (which-key-mode))
#+END_SRC
**** Kill buffer
When killing a buffer always pick the current buffer
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
  (defun kill-current-buffer ()
    "Kills the current buffer."
    (interactive)
    (kill-buffer (current-buffer)))
  (global-set-key (kbd "C-x k") 'kill-current-buffer)
#+END_SRC
**** Final pieces
#+BEGIN_SRC emacs-lisp :exports code :tangle ~/.emacs
(provide '.emacs)
#+END_SRC

